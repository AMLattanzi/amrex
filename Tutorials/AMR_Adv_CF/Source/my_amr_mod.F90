module my_amr_module

  use iso_c_binding
  use amrex_amr_module
  use amrex_fort_module, only : rt => amrex_real

  implicit none

  ! runtime parameters
  integer :: verbose    = 0

  integer :: max_step   = huge(1)
  integer :: regrid_int = 2
  integer :: check_int  = -1
  integer :: plot_int   = -1
  !
  logical :: do_reflux  = .true.
  !
  real(rt) :: stop_time  = huge(1._rt)
  real(rt) :: cfl        = 0.7_rt
  !
  character(len=127) :: check_file = "chk"
  character(len=127) :: plot_file  = "plt"
  character(len=127) :: restart    = ""  

  integer, allocatable :: stepno(:)
  integer, allocatable :: nsubsteps(:)
  integer, allocatable :: last_regrid_step(:)

  real(rt), allocatable :: t_new(:)
  real(rt), allocatable :: t_old(:)
  real(rt), allocatable :: dt(:)

  type(amrex_multifab), allocatable :: phi_new(:)
  type(amrex_multifab), allocatable :: phi_old(:)

  ! type(amrex_fluxregister), allocatable :: flux_reg(:)
  
contains

  subroutine my_amr_init ()
    type(amrex_parmparse) :: pp
    integer :: ilev

    if (.not.amrex_famrcore_initialized()) call amrex_famrcore_init()
    
    call amrex_init_virtual_functions (c_funloc(my_make_new_level_from_scratch), &
         &                             c_funloc(my_error_estimate))

    ! Read parameters
    call amrex_parmparse_build(pp)
    call pp%query("max_step", max_step)
    call pp%query("stop_time", stop_time)
    call amrex_parmparse_destroy(pp)
    
    ! Parameters amr.*
    call amrex_parmparse_build(pp, "amr")
    call pp%query("regrid_int", regrid_int)
    call pp%query("check_int", check_int)
    call pp%query("plot_int", plot_int)
    call pp%query("check_file", check_file)
    call pp%query("plot_file", plot_file)
    call pp%query("restart", restart)
    call amrex_parmparse_destroy(pp)
    
    ! Parameters myamr.*
    call amrex_parmparse_build(pp, "myamr")
    call pp%query("v", verbose)
    call pp%query("verbose", verbose)
    call pp%query("cfl", cfl)
    call pp%query("do_reflux", do_reflux)
    call amrex_parmparse_destroy(pp)

    allocate(stepno(0:amrex_max_level))
    stepno = 0

    allocate(nsubsteps(0:amrex_max_level))
    nsubsteps(0) = 1
    do ilev = 1, amrex_max_level
       nsubsteps(ilev) = amrex_ref_ratio(ilev-1)
    end do

    allocate(last_regrid_step(0:amrex_max_level))
    last_regrid_step = 0

    allocate(t_new(0:amrex_max_level))
    t_new = 0.0_rt

    allocate(t_old(0:amrex_max_level))
    t_old = -1.0e100_rt

    allocate(dt(0:amrex_max_level))
    dt = 1.e100

    allocate(phi_new(0:amrex_max_level))
    allocate(phi_old(0:amrex_max_level))

    ! allocate(flux_reg(0:amrex_max_level))

  end subroutine my_amr_init


  subroutine my_amr_finalize ()
    integer :: lev
    do lev = 0, amrex_max_level
       call amrex_multifab_destroy(phi_new(lev))
       call amrex_multifab_destroy(phi_old(lev))
       ! call amrex_fluxregister_destroy(flux_reg(lev))
    end do
  end subroutine my_amr_finalize

  subroutine my_make_new_level_from_scratch (lev, time, pba, pdm) bind(c)
    use prob_module, only : init_prob_data
    integer, intent(in), value :: lev
    real(amrex_real), intent(in), value :: time
    type(c_ptr), value :: pba, pdm

    type(amrex_boxarray) :: ba
    type(amrex_distromap) :: dm
    type(amrex_mfiter) :: mfi
    type(amrex_box) :: bx
    real(amrex_real), contiguous, pointer :: phi(:,:,:,:)
    integer, parameter :: ncomp = 1, nghost = 0

    ba%p = pba
    dm%p = pdm

    t_new(lev) = time
    t_old(lev) = time - 1.e200_amrex_real
  
    call amrex_multifab_build(phi_new(lev), ba, dm, ncomp, nghost)
    call amrex_multifab_build(phi_old(lev), ba, dm, ncomp, nghost)

!    if (lev > 0 .and. do_reflux) then
!       call amrex_fluxregister_destroy(flux_reg(lev))
!       call amrex_fluxregister_build(flux_reg(lev), ba, dm, amrex_ref_ratio(lev-1), lev, ncomp)
!    end if

    call amrex_mfiter_build(mfi, phi_new(lev))

    do while (mfi%next())
       bx = mfi%tilebox()
       phi => phi_new(lev)%dataptr(mfi)
       call init_prob_data(lev, t_new(lev), bx%lo, bx%hi, phi, lbound(phi), ubound(phi), &
            amrex_geom(lev)%dx, amrex_geom(lev)%problo)
    end do

    call amrex_mfiter_destroy(mfi)

  end subroutine my_make_new_level_from_scratch

  subroutine my_error_estimate (lev, cp, t, settag, cleartag) bind(c)
    use tagging_module, only : tag_phi_error
    integer, intent(in), value :: lev
    type(c_ptr), intent(in), value :: cp
    real(amrex_real), intent(in), value :: t
    character(c_char), intent(in), value :: settag, cleartag

    real(amrex_real), allocatable, save :: phierr(:)
    type(amrex_parmparse) :: pp
    type(amrex_tagboxarray) :: tag
    type(amrex_mfiter) :: mfi
    type(amrex_box) :: bx
    real(amrex_real), contiguous, pointer :: phiarr(:,:,:,:)
    character(c_char), contiguous, pointer :: tagarr(:,:,:,:)

    if (.not.allocated(phierr)) then
       call amrex_parmparse_build(pp, "myamr")
       call pp%getarr("phierr", phierr)
       call amrex_parmparse_destroy(pp)
    end if

    tag%p = cp

    !$omp parallel private(mfi, bx, phiarr, tagarr)
    call amrex_mfiter_build(mfi, phi_new(lev), tiling=.true.)
    do while(mfi%next())
       bx = mfi%tilebox()
       phiarr => phi_new(lev)%dataptr(mfi)
       tagarr => tag%dataptr(mfi)
       call tag_phi_error(bx%lo, bx%hi, &
            phiarr, lbound(phiarr), ubound(phiarr), &
            tagarr, lbound(tagarr), ubound(tagarr), &
            phierr(lev+1), settag, cleartag)  ! +1 because level starts with 0, but phierr starts with 1
    end do
    call amrex_mfiter_destroy(mfi)
    !$omp end parallel

  end subroutine my_error_estimate
  
end module my_amr_module
