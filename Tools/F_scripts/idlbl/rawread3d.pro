function rawread3d, filename, XCOORDS=x, YCOORDS=y, ZCOORDS=z, TIME=time, $
                  XMIN=xmin, YMIN=ymin, ZMIN=zmin, $
                  XMAX=xmax, YMAX=ymax, ZMAX=zmax

; read in a raw single variable block of data generated by the fSlice
; fIDLdump too.
;
; xlength should give the size of the computational domain in the x
; direction, and likewise for ylength -- these are used to build the 
; coordinate information for better contouring.

openr, lun, filename, /get_lun, /f77_unformatted, /swap_endian

; read in the domain extrema
xmin = dblarr(1)
xmax = dblarr(1)
ymin = dblarr(1)
ymax = dblarr(1)
zmin = dblarr(1)
zmax = dblarr(1)

readu, lun, xmin, ymin, xmax, ymax, zmin, zmax

xmin = xmin[0]
xmax = xmax[0]
ymin = ymin[0]
ymax = ymax[0]
zmin = zmin[0]
zmax = zmax[0]

; read in the number of grid points in each direction
xsz = lonarr(1)
ysz = lonarr(1)
zsz = lonarr(1)

readu, lun,  xsz, ysz, zsz
xsz = xsz[0]
ysz = ysz[0]
zsz = zsz[0]

; read in the time
time = dblarr(1)
readu, lun, time
time = time[0]

; read in the data
data = dblarr(xsz, ysz, zsz)
readu, lun, data

; compute the coordinates
dx = (xmax-xmin)/double(xsz)
dy = (ymax-ymin)/double(ysz)
dz = (zmax-zmin)/double(zsz)

x = (findgen(xsz) + 0.5)*dx + xmin
y = (findgen(ysz) + 0.5)*dy + ymin
z = (findgen(zsz) + 0.5)*dz + zmin

close, lun
free_lun, lun

return, data
end
