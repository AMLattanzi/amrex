function rawread, filename, XCOORDS=x, YCOORDS=y, TIME=time, $
                  XMIN=xmin, YMIN=ymin, XMAX=xmax, YMAX=ymax

; read in a raw single variable block of data generated by the fSlice
; fIDLdump too.
;
; xlength should give the size of the computational domain in the x
; direction, and likewise for ylength -- these are used to build the 
; coordinate information for better contouring.

openr, lun, filename, /get_lun, /f77_unformatted

; read in the domain extrema
xmin = dblarr(1)
xmax = dblarr(1)
ymin = dblarr(1)
ymax = dblarr(1)

readu, lun, xmin, ymin, xmax, ymax

xmin = xmin[0]
xmax = xmax[0]
ymin = ymin[0]
ymax = ymax[0]

; read in the number of grid points in each direction
xsz = lonarr(1)
ysz = lonarr(1)

readu, lun,  xsz, ysz
xsz = xsz[0]
ysz = ysz[0]

; read in the time
time = dblarr(1)
readu, lun, time
time = time[0]

; read in the data
data = dblarr(xsz, ysz)
readu, lun, data

; compute the coordinates
dx = (xmax-xmin)/double(xsz)
dy = (ymax-ymin)/double(ysz)

x = (findgen(xsz) + 0.5)*dx
y = (findgen(ysz) + 0.5)*dy

close, lun
free_lun, lun

return, data
end
